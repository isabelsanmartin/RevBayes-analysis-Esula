######################################################################
# 
# APPENDIX S3
#
# Lineage-specific birth-death (LSBDS) model
# RevBayes Analysis: Bayesian inference of diversification rates under 
#		     a conditioned birth-death-shift model
#
# Authors: Sebastian HÃ¶hna
#
#######################################################################

#######################
# Reading in the Data #
#######################

# Read in the observed phylogeny
observed_phylogeny <- readTrees("Esula_tree_no_outgroups.tre")[1]

# Get the names of the taxa in the tree and the age of the tree. We need these later on.
taxa <- observed_phylogeny.taxa()
root <- observed_phylogeny.rootAge()
tree_length <- observed_phylogeny.treeLength()

# Create some vector for the moves and monitors of this analysis
moves    = VectorMoves()
monitors = VectorMonitors()

# Global parameters for our analysis
NUM_TOTAL_SPECIES     = 489
NUM_RATE_CATEGORIES   = 6
H = 0.587405

####################
# Create the rates #
####################

# Specify a prior on the speciation and extinction rates
rate_mean <- (NUM_TOTAL_SPECIES-2) / tree_length

# Create a lognormal distributed variable for the mean speciation rate
speciation_mean ~ dnLoguniform( 1E-6, 1E2)
speciation_mean.setValue( rate_mean )
moves.append( mvScale(speciation_mean, lambda=1, tune=true, weight=2.0) )

speciation_sd ~ dnExponential( 1.0 / H )
moves.append( mvScale(speciation_sd, lambda=1, tune=true, weight=2.0) )

# Create a deterministic variable for the speciation rate categories
# using a discretized lognormal distribution (the N-quantiles of it)
speciation := fnDiscretizeDistribution( dnLognormal(ln(speciation_mean), speciation_sd), NUM_RATE_CATEGORIES )

# Create a lognormal distributed variable for the mean extinction rate
extinction_mean ~ dnLoguniform( 1E-6, 1E2)
extinction_mean.setValue( rate_mean / 2.0 )
moves.append( mvScale(extinction_mean, lambda=1, tune=true, weight=2.0) )

# Create a deterministic variable for the speciation rate categories
# Assume that all extinction rates are equal
extinction := rep(extinction_mean, NUM_RATE_CATEGORIES)

# Create a exponential distributed variable for the rate of rate-shift events
event_rate ~ dnUniform(0.0, 100.0/tree_length)
moves.append( mvScale(event_rate, lambda=1, tune=true, weight=2.0) )

# Create a uniformly distributed variable for the category at the root
rate_cat_probs <- simplex( rep(1, NUM_RATE_CATEGORIES) )

# Rho is the probability of sampling species at the present
# Fix this to 322/489, since there are 489 described species of E. subg Esula
# Rho and we have sampled 322
rho <- observed_phylogeny.ntips()/489

# Construct a variable for the tree drawn from a birth death process
timetree ~ dnCDBDP( rootAge           = root,
                    speciationRates   = speciation,
                    extinctionRates   = extinction,
                    Q                 = fnJC(NUM_RATE_CATEGORIES),
                    delta             = event_rate,
                    pi                = rate_cat_probs,
                    rho               = rho,
                    condition         = "time" )

# Clamp the model with the "observed" tree
timetree.clamp(observed_phylogeny)

#############
# The Model #
#############

# Workspace model wrapper
mymodel = model(speciation)

# Set up the monitors that will output parameter values to file and screen
monitors.append( mnScreen(printgen=10, event_rate, speciation_mean, extinction_mean) )
monitors.append( mnStochasticBranchRate(cdbdp=timetree, printgen=1, filename="output_BDS_Esula_15000generaciones/BDS_Esula_rates.log") )
monitors.append( mnModel(filename="output_BDS_Esula_15000generaciones/BDS_Esula.log",printgen=1, separator = TAB) )

################
# The Analysis #
################

# Workspace MCMC
mymcmc = mcmc(mymodel, monitors, moves, nruns=2, combine="mixed")

# Run the MCMC
mymcmc.run(generations=15000, tuningInterval=200)

##############################################################################
# Bayes Factor Comparison: Include code for path and stepping-stone sampling #
##############################################################################

# Compute power posterior distributions
pow_p = powerPosterior(mymodel, moves, monitors, "output_BDS_Esula_15000generaciones/pow_p_BDS.out", cats=50, sampleFreq=100) 
# pow_p.burnin(generations=3750, tuningInterval=100)
pow_p.run(generations=1000)  

# Use path-sampling to calculate marginal likelihoods
ps = pathSampler(file="output_BDS_Esula_15000generaciones/pow_p_BDS.out", powerColumnName="power", likelihoodColumnName="likelihood")
ps.marginal()

# Use stepping-stone sampling to calculate marginal likelihoods
ss = steppingStoneSampler(file="output_BDS_Esula_15000generaciones/pow_p_BDS.out", powerColumnName="power", likelihoodColumnName="likelihood")
ss.marginal()

# q()
